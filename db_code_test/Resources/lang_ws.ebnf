file := [ws], ( table_definition | fact_definition), {[ws], (table_definition | fact_definition ) }, [ws];

table_definition := "TABLE", ws, table_name, [ws], [ inheritance_specification, [ws]] , (round_body | curly_body) , [ws], ";" 
		;

fact_definition := "FACT", {nonsemicolon_span}, ";" 
	;

nonsemicolon_char := ac - ";" ;
nonsemicolon_span := ( ws | nonsemicolon_char ), { ws | nonsemicolon_char };

table_name := identifier, [ ws, "as" , ws, class_name ];

class_name := identifier;

round_body := "(", [ws], [ comma_separated_field_list, [ws] ], ")";
curly_body := "{", [ws], [ semicolon_separated_field_list, [ws] ], "}";

comma_separated_field_list := member_definition, {[ws], ",", [ws], member_definition } ;

semicolon_separated_field_list := member_definition, [ws], ";", { [ws], member_definition, [ws], ";" } ;

inheritance_specification := ":", [ws], inheritance_specification_list;
inheritance_specification_list := inheritance_specification_list, [ws], ",", [ws], identifier
	| identifier
	;

member_definition := field_definition
	| aggregation_definition
	| set_definition
	| array_definition
	;

(* "INTEGER" -> "DECIMAL", INTEGERS for keys are implied *)
field_definition := ( "INTEGER" | "RATIONAL" | "STRING" | "DATE" ), ws, identifier, [ws, "as" , ws, field_name ] ;

field_name := identifier;

aggregation_definition := identifier, ws, identifier, [ws, "as" , ws, field_name ]
		| "REF", [ws], "<", [ws], identifier, [ws], ">", ws, identifier, [ws, "as" , ws, field_name ]
		(* REF мы ставим, чтобы подчеркнуть, что с другой стороны там соответствующий идентификатору SET или ARRAY
		и поэтому надо делать не три таблицы, а только две parent/child *)
		;

set_definition := "SET", [ws], "<", [ws], identifier, [ws], ">", ws, identifier, [ ws, "as", ws, identifier ];

(* что если позиция в множестве важна? *)
array_definition := "ARRAY", [ws], "<", [ws], identifier, [ws], ">", ws, identifier, [ ws, "as", ws, identifier ];

list_of_levels := list_of_levels, [ws], ",", [ws], identifier
	| identifier
	; 

identifier := letter , { letter | digit | "_" } ;


comment_start := "/*" ;
comment_unit_1 := ( ac - "*" ) ;
comment_unit_2 := ( ac - "/" ) ;
comment_unit := "*", comment_unit_2 | comment_unit_1;
comment_end := "*/" ;
block_comment := comment_start , { comment_unit }, comment_end ;
comment_unit_3 := ( ac - cr ) - lf ;
line_comment := "//", { comment_unit_3 }, line_separator_raw;
comment := line_comment | block_comment;

symbol := "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
       | "=" | "|" | "." | "," | ";"
       | "*" | ":" | "-" | "?" | "_"
       ;

tab := "\x0009" ;
(* \r\n 0D0A (hex), 13,10 (decimal) *)
lf := "\x000A" ; (* \r *)
cr := "\x000D" ; (* \n *)
space := "\x0020" ;
dquote := "\x0022" ;
quote := "\x0027" ;
backslash := "\x005C" ;
eol := ? Terminals.Eol ? ;
ac := ? Terminals.AnyChar ? ;

space_or_tab := ( space | tab ) ;
line_separator_raw := ( cr, lf ) | cr | lf ;
sp := space_or_tab, {space_or_tab} ;
sp_with_linebreak_raw := line_separator_raw | comment ;
ws_unit := space_or_tab | sp_with_linebreak_raw;
ws := ws_unit, {ws_unit} ;

letter := "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;

digit := "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

digit_hex := digit 
		| "A" | "B" | "C" | "D" | "E" | "F"
		| "a" | "b" | "c" | "d" | "e" | "f"
		;

empty_sequence := ;