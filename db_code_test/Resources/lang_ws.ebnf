file := [ws], ( table_definition | fact_definition), {[ws], (table_definition | fact_definition ) }, [ws];

table_definition := [access_modifier, ws], "TABLE", ws, table_name, [ws], [ inheritance_specification, [ws]] , "{", [ws], [ semicolon_separated_field_list, [ws] ], "}" , [ws], ";" ;
fact_definition := "FACT", [ ws, identifier, [ws], "="], ws, identifier, [ws], object_initializer, [ws], ";" ;

access_modifier := "NONGUARDED" | "NONVERSIONED";

table_name := identifier, [ ws, "as" , ws, class_name ];

class_name := identifier;

comma_separated_field_list := member_definition, {[ws], ",", [ws], member_definition } ;

semicolon_separated_field_list := member_definition, [ws], ";", { [ws], member_definition, [ws], ";" } ;

inheritance_specification := ":", [ws], inheritance_specification_list;
inheritance_specification_list := inheritance_specification_list, [ws], ",", [ws], identifier
	| identifier
	;

member_definition := field_definition
	| aggregation_definition
	| set_definition
	| array_definition
	;

(* "INTEGER" -> "DECIMAL", INTEGERS for keys are implied *)
field_definition := raw_type, ws, identifier, [ws, "as" , ws, field_name ] ;

raw_type := "INTEGER" | "RATIONAL" | "STRING" | "DATE";

field_name := identifier;

aggregation_definition := identifier, ws, identifier, [ws, "as" , ws, field_name ]
		| "REF", [ws], "<", [ws], identifier, [ws], ">", ws, identifier, [ws, "as" , ws, field_name ]
		(* REF мы ставим, чтобы подчеркнуть, что с другой стороны там соответствующий идентификатору SET или ARRAY
		и поэтому надо делать не три таблицы, а только две parent/child *)
		;

set_definition := "SET", [ws], "<", [ws], item_type, [ws], ">", ws, db_name, [ ws, "as", ws, code_name ];

array_definition := "ARRAY", [ws], "<", [ws], item_type, [ws], ">", ws, db_name, [ ws, "as", ws, code_name ];

item_type := identifier;
db_name := identifier;
code_name := identifier;

list_of_levels := list_of_levels, [ws], ",", [ws], identifier
	| identifier
	; 

object_initializer := "(", [ws], field_initializer, { [ws], ",", [ws], field_initializer } , [ws], ")" ;

field_initializer := identifier, [ws], "=", [ws], (initializer_item | set_initializer | array_initializer) ;

initializer_item := identifier | integer | rational | string | date ;
set_initializer := "{", [[ws], initializer_item, { [ws], ",", [ws], initializer_item }], [ws], "}" ;
array_initializer := "<", [[ws], initializer_item, { [ws], ",", [ws], initializer_item }], [ws], ">" ;

identifier := letter , { letter | digit | "_" } ;

string := quote, [string_element2, { string_element2 }] , quote
         | dquote, [string_element1, { string_element1}] , dquote ;
string_element1 = escape_seq | (ac - dquote);
string_element2 = escape_seq | (ac - quote);
escape_seq := backslash, "x", 4 * [digit_hex], [";"] ;


date := [ 2 * digit ], 2 * digit, ["-", [digit], digit, ["-", [digit], digit]] ;

integer := [ "-" ], nonnegative_integer;

nonnegative_integer := digit, {digit} ;

rational := integer, "/", nonnegative_integer;


comment_start := "/*" ;
comment_unit_1 := ( ac - "*" ) ;
comment_unit_2 := ( ac - "/" ) ;
comment_unit := "*", comment_unit_2 | comment_unit_1;
comment_end := "*/" ;
block_comment := comment_start , { comment_unit }, comment_end ;
comment_unit_3 := ( ac - cr ) - lf ;
line_comment := "//", { comment_unit_3 }, line_separator_raw;
comment := line_comment | block_comment;

rightwards_open_headed_arrow := "\x21FE" ; (* ⇾ *)

tab := "\x0009" ; (* \t *)
lf := "\x000A" ; (* \r *)
cr := "\x000D" ; (* \n *)
space := "\x0020" ;
dquote := "\x0022" ;
quote := "\x0027" ;
backslash := "\x005C" ;
eol := ? Terminals.Eol ? ;
ac := ? Terminals.AnyChar ? ;

space_or_tab := ( space | tab ) ;
(* \r\n 0D0A (hex), 13,10 (decimal) *)
line_separator_raw := ( cr, lf ) | cr | lf ;
sp := space_or_tab, {space_or_tab} ;
sp_with_linebreak_raw := line_separator_raw | comment ;
ws_unit := space_or_tab | sp_with_linebreak_raw;
ws := ws_unit, {ws_unit} ;

letter := "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;

digit := "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

digit_hex := digit 
		| "A" | "B" | "C" | "D" | "E" | "F"
		| "a" | "b" | "c" | "d" | "e" | "f"
		;
